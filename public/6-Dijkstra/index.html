
    <!DOCTYPE html>
    <html lang="en">
    <head>
        <meta charset="UTF-8">
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <meta name="description" content="Es un algoritmo para la determinación del camino más corto, dado un vértice origen, hacia el resto de los vértices en un grafo que tiene pesos en cada arista. En este apartado, se realizó la implementación en Haskell." />
        <link href="../assets/main.css" rel="stylesheet" type="text/css"/>
        <title>Implementación del algoritmo de Dijkstra</title>
    </head>
    <body>
        <header>
            <a href="/"><img class="arrow-icon" src="../assets/img/arrow-back.png" alt="arrow icon"></a>
        </header>
        <div class="content">
            <h1>Implementación del algoritmo de Dijkstra</h1>
            <hr />
            <p>Es un algoritmo para la determinación del camino más corto, dado un vértice origen, hacia el resto de los vértices en un grafo que tiene pesos en cada arista.</p>
<h2 id="ambiente-de-programación">Ambiente de programación</h2>
<p>El programa fue desarrollado en un ambiente <strong>interpretado</strong> e interactivamente. Esto con la ayuda de la herramienta <strong>GHCi</strong> el cual permite evaluar expresiones de Haskell interactivamente. Haskell de igual manera permite la <strong>compilación</strong> de sus programas al generar ejectuables (.exe).
El proceso para compilar un archivo (.hs) es muy similar a otros lenguajes. Se necesita tener el compilador instalado <strong>GHC</strong> y ejecutar:</p>
<pre><code class="language-haskell">ghc -o nombre-salida archivo.hs</code></pre>
<p>Esto generará un ejecutable con el nombre de salida.</p>
<p>Para generar un ambiente interactivo al momento de querer <em>interpretar</em> el código se requiere de <strong>ghci</strong> mencionado anteriormente y ejecutar:</p>
<pre><code class="language-haskell">ghci
Prelude&gt; :l archivo.hs //Para CORRER un archivo</code></pre>
<h2 id="aplicación">Aplicación</h2>
<p><em>Desarrollar un programa que calcule la distancia más corta entre dos nodos de un grafo. El programa recibirá como entrada la especificación de un grafo (nodos y enlaces con peso, el nodo inicial y el nodo final). La salida del programa será la lista de nodos desde el inicial hasta el final y el peso de ese camino que debe ser el mínimo.</em></p>
<h3 id="entrada">Entrada</h3>
<p>El programa recibe como entrada un archivo de texto con 3 columnas indicando a los nodos junto con sus enlaces donde las primeras 2 columnas son los nodos involucrados y la tercera el peso del enlace.abnf</p>
<pre><code>1 2 4
1 3 3
2 5 8
3 4 12
3 6 4
4 7 20
4 8 15
4 6 2
5 7 17
6 8 22
7 8 9</code></pre><h3 id="salida">Salida</h3>
<p>El programa cuenta con las funciones para poder utilizar GHCi con el objetivo de poder consultar varias rutas a distintos nodos finales.</p>
<pre><code class="language-haskell">ghci
Prelude&gt; :l main.hs                     //Compilar
Prelude&gt; txt &lt;- readFile &quot;graph.txt&quot;    //Leer archivo del grafo
Prelude&gt; let g = fromText txt False     //Almacenar un pseudo-grafo NO dirigido a partir del texto
Prelude&gt; edgesFor g &quot;1&quot;                 //Imprimir los enlaces del nodo &quot;1&quot;
Prelude&gt; let graph = dijkstra g &quot;1&quot;     //Utilizar el algoritmo para conocer las rutas a partir del nodo &quot;1&quot;
Prelude&gt; pathToNode graph &quot;8&quot;           //Imprimir la ruta más corta desde &quot;1&quot; hasta &quot;8&quot;</code></pre>
<h3 id="código">Código</h3>
<p>El código cuenta con listas dinámicas como estructuras de datos utilizadas y son proporcionadas por el paquete de haskell. Así como también cuenta con las clases de <em>Graph, Edge, Node DNode</em>.</p>
<pre><code class="language-haskell">
module Dijkstra 
(
  fromText,
  dijkstra,
  pathToNode,
  edgesFor,
  Edge(..),
  Node,
  Graph,
  Dnode
) where

import Data.List

data Edge = Edge { node::Node, weight::Float } deriving (Show)
type Node = String
type Graph = [(Node, [Edge])]
type Dnode = (Node, (Float, Node))

fromText :: String -&gt; Bool -&gt; Graph
fromText strLines isDigraph = 
  let readData [n1, n2, w] = ((n1, n2), read w :: Float)
      es = map (readData . words) $ lines strLines
      allEs = if isDigraph then es 
              else appendReversed es
  in fromList allEs

appendReversed :: [((String, String), Float)] -&gt; [((String, String), Float)]
appendReversed es = es ++ map (\((n1,n2),w) -&gt; ((n2,n1),w)) es

fromList :: [((String, String), Float)] -&gt; Graph
fromList es =
  let nodes = nub . map (fst . fst) $ es
      edgesFor es node = 
        let connected = filter (\((n,_),_) -&gt; node == n) $ es
        in map (\((_,n),wt) -&gt; Edge n wt) connected 
  in map (\n -&gt; (n, edgesFor es n)) nodes

edgesFor :: Graph -&gt; Node -&gt; [Edge]
edgesFor g n = snd . head . filter (\(nd, _) -&gt; nd == n) $ g

weightFor :: Node -&gt; [Edge] -&gt; Float
weightFor n = weight . head . filter (\e -&gt; n == node e)

connectedNodes :: [Edge] -&gt; [Node]
connectedNodes = map node

dnodeForNode :: [Dnode] -&gt; Node -&gt; Dnode
dnodeForNode dnodes n = head . filter (\(x, _) -&gt; x == n) $ dnodes

dijkstra :: Graph -&gt; Node -&gt; [Dnode]
dijkstra g start = 
  let dnodes = initD g start
      unchecked = map fst dnodes
  in  dijkstra&#39; g dnodes unchecked

initD :: Graph -&gt; Node -&gt; [Dnode]
initD g start =
  let initDist (n, es) = 
        if n == start 
        then 0 
        else if start `elem` connectedNodes es
             then weightFor start es
             else 1.0/0.0
  in map (\pr@(n, _) -&gt; (n, ((initDist pr), start))) g

dijkstra&#39; :: Graph -&gt; [Dnode] -&gt; [Node] -&gt; [Dnode]
dijkstra&#39; g dnodes [] = dnodes
dijkstra&#39; g dnodes unchecked = 
  let dunchecked = filter (\dn -&gt; (fst dn) `elem` unchecked) dnodes
      current = head . sortBy (\(_,(d1,_)) (_,(d2,_)) -&gt; compare d1 d2) $ dunchecked
      c = fst current
      unchecked&#39; = delete c unchecked
      edges = edgesFor g c
      cnodes = intersect (connectedNodes edges) unchecked&#39;
      dnodes&#39; = map (\dn -&gt; update dn current cnodes edges) dnodes
  in dijkstra&#39; g dnodes&#39; unchecked&#39; 

update :: Dnode -&gt; Dnode -&gt; [Node] -&gt; [Edge] -&gt; Dnode
update dn@(n, (nd, p)) (c, (cd, _)) cnodes edges =
  let wt = weightFor n edges
  in  if n `notElem` cnodes then dn
      else if cd+wt &lt; nd then (n, (cd+wt, c)) else dn

pathToNode :: [Dnode] -&gt; Node -&gt; [Node]
pathToNode dnodes dest = 
  let dn@(n, (d, p)) = dnodeForNode dnodes dest
  in if n == p then [n] else pathToNode dnodes p ++ [n]
</code></pre>
<h3 id="capturas">Capturas</h3>
<p><img src="../assets/img/code1.png" alt="Ruta más corta de 1 a 8">
<img src="../assets/img/code2.png" alt="Impresión del grafo"></p>

        </div>

        <footer>
            <div>
                <p>Iconos por <a href="https://www.flaticon.es/autores/becris" title="Becris">Becris</a> from <a href="https://www.flaticon.es/" title="Flaticon"> www.flaticon.es</a></p>
            </div>
            <div>
                <a href="https://github.com/alextorrer/haskell-ssg"><img src="../assets/img/github.png" alt=""></a>
            </div>
        </footer>
    </body>

    </html>
